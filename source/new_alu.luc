module new_alu (
    input firstNumber[16],  // input of 1st number
    input secondNumber[16], // inoput of 2nd number
    input alufn[6],         // input of alufn
    output answer[16],      // output of answer
    output n,               // output of n from adder
    output v,               // output of v from adder
    output z                // output of z from adder
  ) {
    //initialize all the different modules (ALU functions)
    adder adder;
    bool bool;
    shft shft;
    comp comp;
    nv nv;         //computes v,n,z from adder.answer
  
  
  always {
    //sets all values to be ignored by default
    answer = hx;                
    v=bx;
    n=bx;
    z=bx;
    adder.firstNumber = hx;
    adder.secondNumber = hx;
    adder.alufn = hx;
    bool.firstNumber = hx;
    bool.secondNumber = hx;
    bool.alufn = hx;
    shft.firstNumber = hx;
    shft.secondNumber = hx;
    shft.alufn = hx;
    comp.z = hx;
    comp.v = hx;
    comp.n = hx;
    comp.alufn = hx;
    nv.firstNumber = hx;
    nv.secondNumber = hx;
    nv.sum = hx;
    nv.a31 = hx;
    nv.b31 = hx;
    nv.s31 = hx;
    nv.alufn = hx;
    
    //Mux4 with control signals ALUFN[5:4], that chooses output based on operation
    
    //ALUFN[5:4]==00, ADDER
    if(alufn[5] == b0 & alufn[4] == b0){
      adder.firstNumber = firstNumber;
      adder.secondNumber = secondNumber;
      adder.alufn = alufn[1:0];    //ALUFN[1:0] determines whether to add(00), subtract(01), multiply(10) or divide(11)
      answer = adder.answer;
      nv.alufn = alufn[1:0];
      nv.firstNumber = firstNumber;
      nv.secondNumber = secondNumber;
      nv.sum = adder.answer;
      nv.s31=adder.answer[15];
      nv.a31=firstNumber[15];
      nv.b31=secondNumber[15];
      z=nv.z;
      v=nv.v;
      n=nv.n;
    }
    
    //ALUFN[5:4]==01, BOOLEAN
    if(alufn[5] == b0 & alufn[4] == b1){
      bool.firstNumber = firstNumber;
      bool.secondNumber = secondNumber;
      bool.alufn = alufn[3:0];     //ALUFN[3:0] determines which boolean function 
      answer = bool.answer;        //AND(1000), OR(1110), XOR(0110), "A"(1010)
    }
    
    //ALUFN[5:4]==10, SHIFT 
    if(alufn[5] == b1 & alufn[4] == b0){
      shft.firstNumber = firstNumber;
      shft.secondNumber = secondNumber[3:0];
      shft.alufn = alufn[1:0];     //ALUFN[1:0] determines direction of shift and whether with sign
      answer = shft.answer;        //SHL(00), SHR(01), SRA(11)
    }
    
    //ALUFN[5:4]==11, COMPARE
    if(alufn[5] == b1 & alufn[4] == b1){
      adder.firstNumber = firstNumber;
      adder.secondNumber = secondNumber;
      adder.alufn = b01;              //subtracting second number from first number through adder function
      nv.alufn = alufn[1:0];          //ALUFN[1:0] determines how v, n are computed through nv module
      nv.firstNumber = firstNumber;    
      nv.secondNumber = secondNumber;
      nv.sum = adder.answer;
      nv.s31 = adder.answer[15];
      nv.a31 = firstNumber[15];
      nv.b31 = secondNumber[15];      
      comp.z = nv.z;
      comp.v = nv.v;
      comp.n = nv.n;
      comp.alufn = alufn[2:1];        //ALUFN[2:1] determines whether to CMPMT(00), CMPEQ(01), CMPLT(10), CMPLE(11)
      answer = comp.answer;
    }
  }
}